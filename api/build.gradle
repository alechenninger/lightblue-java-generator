buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'io.github.alechenninger.lightblue'
project.version = '0.1.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
  jcenter()
}

sourceSets {
  main {
    java {
      srcDirs = ['src/']
    }
  }

  test {
    java {
      srcDirs = ['test/']
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['ApiPublication']
  pkg {
    repo = 'maven'
    name = project.name
    licenses = ['GPL-3.0']
    vcsUrl = 'https://github.com/alechenninger/lightblue-java-generator.git'
    version {
      name = project.version
      gpg {
        sign = true
        passphrase = project.property('gpgPassphrase')
      }
      mavenCentralSync {
        user = project.property('sonatypeUser')
        password = project.property('sonatypePassword')
      }
    }
  }
}


publishing {
  publications {
    ApiPublication(MavenPublication) {
      from components.java
      groupId project.group
      artifactId project.name
      version project.version
    }
  }
}
